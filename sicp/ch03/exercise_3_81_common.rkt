#lang racket

(provide make-random-state)
(provide process-random-generator-requests)
(provide process-random-generator-crequests)

; interfaces:
; * make-random-state : Int -> RandomState 
; * rand-update: RandomState -> RandomResult
; * rand-state: RandomResult -> RandomState
; * rand-result: RandomResult -> Float (where the return value is in [0,1)
; (`rand-result` and `rand-state` should be used
;  on values of type RandomResult, which is generated by `rand-update`)`

(define (make-random-state seed)
  ; see: http://docs.racket-lang.org/reference/generic-numbers.html#%28part._.Random_.Numbers%29
  (vector->pseudo-random-generator 
    (list->vector
      ; the first three integers are
      ;   in the range 0 to 4294967086, inclusive;
      ; the last three integers are
      ;   in the range 0 to 4294944442, inclusive;
      ; at least one of the first three integers is non-zero;
      ; and at least one of the last three integers is non-zero. 
      (list (remainder seed 4294967087)
            1
            0
            (remainder seed 4294944442)
            1
            0))))

(define default-random-state
  (make-random-state 0))

(define (rand-update rand-state)
  (cons (random rand-state)
        rand-state))

(define rand-state cdr)
(define rand-result car)

; process the stream of requests using the current state specified
(define (process-reqs-with-state reqs cur-state)
  (if (stream-empty? reqs)
    ; nothing left
    empty-stream
    ; else
    (let ((req (stream-first reqs)))
      ; the requests are either `'(generate)` or `'(reset <value>)`
      (cond ((eq? (car req) 'generate)
              ; generate next value
              (let ((result (rand-update cur-state)))
                (stream-cons
                  (rand-result result)
                  (process-reqs-with-state
                    (stream-rest reqs)
                    (rand-state result)))))
            ((eq? (car req) 'reset)
              ; reset stream using new seed
              (process-reqs-with-state
                (stream-rest reqs)
                (make-random-state (cadr req))))))))

; process requests, initializing the state with the default one
(define (process-random-generator-requests reqs)
  (process-reqs-with-state reqs default-random-state))

; compact request streams (crequest) are
;   the flattened version of the request stream
; e.g: requests: '((generate) (reset 1) (generate) ...)
;     crequests: '(generate reset 1 generate ...)
(define (crequests->requests s)
  (if (stream-empty? s)
    empty-stream
    (let ((req (stream-first s)))
      (cond ((eq? req 'generate)
              (stream-cons
                (list 'generate)
                (crequests->requests
                  (stream-rest s))))
            ((eq? req 'reset)
              (stream-cons
                (list 'reset (stream-first (stream-rest s)))
                (crequests->requests
                  (stream-rest (stream-rest s)))))))))

(define (process-random-generator-crequests creqs)
  (process-reqs-with-state
    (crequests->requests creqs)
    default-random-state))
